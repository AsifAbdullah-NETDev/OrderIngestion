CREATE OR ALTER PROCEDURE SP_InsertOrderWithItems
(
    @RequestId UNIQUEIDENTIFIER,
    @OrderNumber NVARCHAR(50),
    @CustomerName NVARCHAR(200),
    @CustomerEmail NVARCHAR(200),
    @Items dbo.OrderItemType READONLY
)
AS
BEGIN
    SET NOCOUNT ON;

    IF EXISTS (SELECT 1 FROM Orders WHERE RequestId = @RequestId)
        RETURN -1;

    DECLARE @CustomerId INT;

    SELECT @CustomerId = CustomerId FROM Customers WHERE Email = @CustomerEmail;

    IF @CustomerId IS NULL
    BEGIN
        INSERT INTO Customers (Name, Email)
        VALUES (@CustomerName, @CustomerEmail);

        SET @CustomerId = SCOPE_IDENTITY();
    END

    INSERT INTO Orders (RequestId, OrderNumber, CustomerId)
    VALUES (@RequestId, @OrderNumber, @CustomerId);

    DECLARE @OrderId INT = SCOPE_IDENTITY();

    INSERT INTO OrderItems (OrderId, SKU, Quantity, Price)
    SELECT @OrderId, SKU, Quantity, Price FROM @Items;

    RETURN @OrderId;
END
